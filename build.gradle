group 'io.puharesource.simplemavenrepo'
version '1.0.0'

buildscript {
    ext.kotlin_version = '1.1.4'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'A Simple Maven Repository',
                'Implementation-Version': version,
                'Main-Class': 'io.puharesource.simplemavenrepo.SimpleMavenRepoKt'
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

project.tasks.assemble.dependsOn project.tasks.fatJar
jar.enabled = false

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir/out/")
}

repositories {
    mavenCentral()
    jcenter()

    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: kotlin_version
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-html-jvm', version: '0.6.3'

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.1'

    compile group: 'com.sparkjava', name: 'spark-kotlin', version: '1.0.0-alpha'
    compile group: 'com.qmetric', name: 'spark-authentication', version: '1.4'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'com.github.salomonbrys.kotson', name: 'kotson', version: '2.5.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    repositories {
        maven {
            credentials {
                username "$mavenUser"
                password "$mavenPassword"
            }

            url 'http://repo.puharesource.io/repo/'
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}